// TODO: Support for enums
const OpNil := 0;
const OpAdd := 1;
const OpSub := 2;
const OpMul := 3;
const OpDiv := 4;

const TreeNode := struct {
	var operation: int;
	var number:    int;

	var left:  *TreeNode;
	var right: *TreeNode;
}

const operation := proc(var a: *TreeNode, var op: int, var b: *TreeNode) -> *TreeNode {
	var n: *TreeNode = allocate(size_of(TreeNode));
	n.operation = op;
	n.left = a;
	n.right = b;
	n.number = 0;
	return n;
}

const number := proc(var number: int) -> *TreeNode {
	var a: *TreeNode = allocate(size_of(TreeNode));
	a.operation = OpNil;
	a.number = number;
	a.left = null;
	a.right = null;
	return a;
}

const expr_eval := proc(var root: *TreeNode) -> int {
	if root.operation != OpNil {
		var operation := root.operation;
		var a  := expr_eval(root.left);
		var b  := expr_eval(root.right);
		
		var c: int;

		if operation == OpAdd then
			c = a + b;
		else if operation == OpSub then
			c = a - b;
		else if operation == OpMul then
			c = a * b;
		else if operation == OpDiv then
			c = a / b;
		else
			c = 0;

		return c;
	}

	return root.number;
}

const expr_print := proc(var e: *TreeNode, var t: int) {
	for var i:=0; i<t; i+=1 { print(" "); }

	if e.operation == OpNil {
		print("%\n", e.number);
		return;
	}

	if e.operation == OpAdd then
	 	print("+");
	else if e.operation == OpSub then
		print("-");
	else if e.operation == OpMul then
		print("*");
	else if e.operation == OpDiv then
		print("/");
	print("\n");

	expr_print(e.left, t + 3);
	expr_print(e.right, t + 3);
}

const expr_create := proc() -> *TreeNode {
	var a : *TreeNode;

	a = operation(number(5), OpAdd, number(6));
	a = operation(a, OpSub, number(2));
	a = operation(a, OpMul, number(3));

	var b : *TreeNode;
	b = operation(number(25), OpDiv, number(5));

	return operation(a, OpAdd, b);
}

const main := proc() {
	var expr := expr_create();
	print("The expression is \n");
	expr_print(expr, 0);
	print("\nThe evaluated result is %\n", expr_eval(expr));
}
